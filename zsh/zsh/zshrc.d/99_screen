# start tmux/screen if not already in it or running a dumb terminal

# 'rxvt' is needed for dvtm
if [[ ! "$TERM" =~ ^\(screen.*\|rxvt\|dumb\|vt100\)$ ]] \
   && [[ -z "$TMUX" ]] \
   && [[ -z "$TMUX_DISABLE" ]] \
   && [[ -z "$ZSHRUN" ]] && [[ -z "$VIM" ]]; then
    export ORIGTERM=$TERM
    export ORIGCOLORTERM=$COLORTERM
    unset TMUX
    if have -c tmux; then
        tmux new-session
    else
        screen -m
    fi
fi

# update session environment variables if they got changed after re-attaching
# a tmux session
if [[ -n "$TMUX" ]]; then
    _update_from_tmux() {
        # Remove need to detach/re-attach after using session in different client
        # See also https://groups.google.com/forum/#!topic/tmux-users/UZFikgdglJU
        if is-at-least 2.4 "$TMUX_VERSION"; then
            tmux switch-client -t $(tmux display-message -p -F '#{client_name}')
        else
            tmux switch-client
        fi
        VARS="$(tmux show-environment)"
        for VAR in ${(f)VARS}; do
            # only export non-removed variables
            if [[ "${VAR#-}" == "${VAR}" ]]; then
                export "$VAR"
            else
                unset "${VAR#-}"
            fi
        done
    }
    add-zsh-hook precmd  _update_from_tmux
    add-zsh-hook preexec _update_from_tmux
fi

# vim: filetype=zsh
