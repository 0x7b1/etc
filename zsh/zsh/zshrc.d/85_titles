if is-at-least 4.3.4; then

__set_screen_title() {
    if [[ "$TERM" == screen* ]]; then
        printf "\ek%s\e\\" ${(V)argv}
    fi
}
_update_screen_title() {
    if [[ -n "$1" ]]; then
        __set_screen_title "${1}"
    elif [[ -n "$SSH_CLIENT" ]]; then
        __set_screen_title "${(%):-%m}"
    else
        __set_screen_title "zsh"
    fi
}
add-zsh-hook precmd _update_screen_title


__set_xterm_title() {
    if [[ "$TERM" == (xterm*|rxvt*|screen*) ]]; then
        printf "\e]0;%s\e\\" ${(V)argv}
    fi
}
_update_xterm_title() {
    __set_xterm_title "${(%):-%n@%m: ${1:+${1} | }%~}"
}
add-zsh-hook precmd _update_xterm_title


__simplify_cmd() {
    # get first word of command that doesn't match the pattern
    local pattern="^(*=*|sudo|(auto)?ssh|-*)"
    local cmd="${1[(wr)${pattern}]}"

    # only retain part after last /
    cmd="${cmd//*\//}"

    # if running under ssh append @host
    if [[ -n $SSH_CLIENT ]]; then
        cmd+="@${(%):-%m}"
    fi

    print -n "$cmd"
}

# adapted from
# http://git.madduck.net/v/etc/zsh.git/blob/HEAD:/.zsh/zshrc/85_xtermtitle
_update_exec_titles() {
    # split up the command line
    local -a cmd; cmd=(${(z)1})

    # Construct a command that will output the desired job number.
    case $cmd[1] in
        fg) if (( $#cmd == 1 )); then
                # No arguments, must find the current job
                cmd=(builtin jobs -l %+)
            else
                # Replace the command name, ignore extra args.
                cmd=(builtin jobs -l ${(Q)cmd[2]})
            fi;;
        %*) cmd=(builtin jobs -l ${(Q)cmd[1]});; # Same as "else" above
        *) # Not resuming a job,
            _update_xterm_title "$cmd"
            _update_screen_title "$(__simplify_cmd "$cmd")"
            return;;                        # so we're all done
    esac

    local -A jt; jt=(${(kv)jobtexts})       # Copy jobtexts for subshell

    # Run the command, read its output, and look up the jobtext.
    # Could parse $rest here, but $jobtexts (via $jt) is easier.
    $cmd >>(
        read num rest
        cmd=(${(z)${(e):-\$jt$num}})
        _update_xterm_title "$cmd"
        _update_screen_title "$(__simplify_cmd "$cmd")"
    )
}
add-zsh-hook preexec _update_exec_titles


# Old version
else # < 4.3.4

precmd() {
    # adjust screen window title
    if [[ "$TERM" == screen* ]]; then
        if [[ -n "$SSH_CLIENT" ]]; then
            print -nP "\ek%m\e\\"
        else
            print -n "\ekzsh\e\\"
        fi
    fi

    # adjust title of xterm
    # see http://www.faqs.org/docs/Linux-mini/Xterm-Title.html
    case $TERM in (xterm*|rxvt*|screen*)
        print -nP "\e]0;%n@%m: %~\e\\"
        ;;
    esac
}

preexec() {
    # get the name of the program currently running and hostname of local machine
    # set screen window title if running in a screen
    if [[ "$TERM" == screen* ]]; then
        local CMD="${1[(wr)^(*=*|sudo|(auto)?ssh|-*)]//*\//}${SSH_CLIENT:+@${HOST//.*/}}"
        print -nr $'\ek'$CMD$'\e\\'
    fi

    # adjust title of xterm
    case $TERM in (xterm*|rxvt*|screen*)
        print -nr $'\e]0;'${USER}@${HOST}: $1$'\e\\'
        ;;
    esac
}

fi

# vim: filetype=zsh
