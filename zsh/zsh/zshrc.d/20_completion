# Whenever a command completion is attempted, make sure the entire command
# path is hashed first.
setopt hash_list_all

# If unset, the cursor is set to the end of the word if completion is
# started. Otherwise it stays there and completion is done from both ends.
setopt complete_in_word

# complete globs instead of expanding them
setopt glob_complete

# load fancy menu completion
zmodload zsh/complist

# initialize completion system
autoload -U compinit
compinit -d $ZSH_CACHEDIR/zcompdump


## general settings
# separate matches into groups
zstyle ':completion:*' group-name ''
# if there is more than 1 match allow selecting from a menu
zstyle ':completion:*' menu select=1
# zstyle ':completion:*' menu yes select interactive
# provide verbose completion information
zstyle ':completion:*' verbose true
# match uppercase from lowercase
if is-at-least 4.3; then
    zstyle ':completion:*' matcher-list 'm:{[:lower:]}={[:upper:]}'
else
    zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
fi
# show ignored matches if we really want to
zstyle ':completion:*' single-ignored show
# caching
zstyle ':completion:*' use-cache yes
zstyle ':completion:*' cache-path $ZSH_CACHEDIR/zcompcache/
# activate color-completion
# see http://www.zsh.org/mla/users/2008/msg00699.html
# zstyle -e ':completion:*:default' list-colors 'reply=(
#     "${PREFIX:+=(#bi)($PREFIX:t)(?)*=00=38;5;243='${color[underline]}'}"
#     ${(s.:.)LS_COLORS}
# )'
zstyle ':completion:*:default' list-colors 'hi=30;41' 'du=37;41' ${(s.:.)LS_COLORS}

zstyle ':completion:*' squeeze-slashes true
zstyle ':completion:*' list-dirs-first true # affects all-files/other-files
zstyle ':completion:*' ignore-parents parent pwd

# prevent asking with many results
# zstyle ':completion:*:default' list-prompt '%p'
zstyle ':completion:*:descriptions' format $'%{\e[0;32m%}completing %B%d%b%{\e[0m%}'
zstyle ':completion:*:messages'     format '%d'
zstyle ':completion:*:warnings'     format $'%{\e[0;31m%}No matches for:%{\e[0m%} %d'
zstyle ':completion:*:options'      auto-description '%d'


## correction
# run rehash on completion so newly installed programs are found automatically
_force_rehash() {
    (( CURRENT == 1 )) && rehash
    return 1 # Because we didn't really complete anything
}
zstyle -e ':completion:*' completer '
if [[ $_last_try != "$HISTNO$BUFFER$CURSOR" ]]; then
    _last_try="$HISTNO$BUFFER$CURSOR"
    reply=(_complete _match _prefix)
else
    if [[ $words[1] == (rm|mv) ]]; then
        reply=(_complete)
    else
        reply=(_oldlist _force_rehash _complete _correct _approximate _ignored)
    fi
fi'
zstyle ':completion:*:correct:*'   insert-unambiguous true
zstyle ':completion:*:correct:*'   original true
zstyle ':completion:*:correct:*'   max-errors 2
zstyle ':completion:*:corrections' format $'%{\e[0;31m%}%d (errors: %e)%{\e[0m%}'
# allow one error for every three characters typed in approximate completer
zstyle ':completion:*:approximate:*' max-errors 'reply=( $(( ($#PREFIX+$#SUFFIX)/3 )) numeric )'


## ZSH stuff
# complete 'cd -<tab>' with menu
zstyle ':completion:*:*:cd:*:directory-stack' menu yes select
# offer indexes before parameters in subscripts
zstyle ':completion:*:*:-subscript-:*'        tag-order indexes parameters
# define files to ignore for zcompile
zstyle ':completion:*:*:zcompile:*'           ignored-patterns '(*~|*.zwc)'
# insert all expansions for expand completer
zstyle ':completion:*:expand:*'               tag-order all-expansions
# Ignore completion functions for commands you don't have:
zstyle ':completion::(^approximate*):*:functions' ignored-patterns '_*'
# don't complete files already in the command line
zstyle ':completion:*:other-files' ignore-line other
zstyle ':completion:*:directories' ignore-line other
# ... except for mv and cp
zstyle ':completion:*:(mv|cp):*:other-files' ignore-line no
zstyle ':completion:*:(mv|cp):*:directories' ignore-line no
# zstyle ':completion:*:(rm|kill|diff):*' ignore-line other

# history word completion (^[/)
zstyle ':completion:*:history-words' list false
zstyle ':completion:*:history-words' menu yes
zstyle ':completion:*:history-words' remove-all-dups yes
zstyle ':completion:*:history-words' stop yes


## external commands
# describe options in full
zstyle ':completion:*:options'                      description 'yes'

# don't complete backup files as executables
zstyle ':completion:*:complete:-command-::commands' ignored-patterns '*\~'
zstyle ':completion:*:*:(^rm):*:*' ignored-patterns '*(.o|~)' '#*#(D)' '*.zwc' '*.pyc' '*.pyo'
zstyle ':completion:*:*:vim:*:*' ignored-patterns $(zstyle -s ':completion:*:*:(^rm):*:*' ignored-patterns pat && echo $pat) \
    '(|**/)tags' '*.aux' '*.pdf' '*.class'

zstyle ':completion:*:processes'          command 'ps xwww --forest -o pid,%cpu,tty,time,command'
zstyle ':completion:*:kill:*'             force-list always
zstyle -e ':completion:*:*:kill:*:processes' list-colors 'reply=(
    "=(#bi) #([0-9]#) #([^ ]#) #([^ ]#) #([^ ]#) ##*($PREFIX)*=0=1;31=35=36=33=1;32"
    "=(#bi) #([0-9]#) #([^ ]#) #([^ ]#) #([^ ]#)*=0=1;31=35=36=33"
)'

zstyle ':completion:*:*:ant:*:targets' call-command true

# complete manual by their section
zstyle ':completion:*:manuals'   separate-sections true
zstyle ':completion:*:manuals.*' insert-sections   true
zstyle ':completion:*:man:*'     menu yes no-select

# make ./configure completion simpler
zstyle ':completion:*:*:-command-:*' ignored-patterns './config.*'

# host completion /* add brackets as vim can't parse zsh's complex cmdlines {{{ */
[[ -r ~/.ssh/known_hosts ]] && _ssh_hosts=(${${${${(f)"$(<$HOME/.ssh/known_hosts)"}:#[\|]*}%%\ *}%%,*}) || _ssh_hosts=()
[[ -r /etc/hosts ]] && : ${(A)_etc_hosts:=${(s: :)${(ps:\t:)${${(f)~~"$(</etc/hosts)"}%%\#*}##[:blank:]#[^[:blank:]]#}}} || _etc_hosts=()
hosts=(
    majutsushi.net
    "$_ssh_hosts[@]"
    "$_etc_hosts[@]"
    $HOST
    localhost
)
zstyle ':completion:*:hosts' hosts $hosts
zstyle ':completion:*:hosts' ignored-patterns 'ip6-*'

zstyle ':completion:*:users' ignored-patterns avahi avahi-autoipd backup bin bitlbee \
    cl-builder colord daemon Debian-exim Debian-gdm festival fetchmail games gnats hplip \
    irc lastfm libuuid list lp mail man messagebus minbif mpd news nobody ntp polkituser \
    proxy saned spampd sshd statd sync sys timidity uml-net usbmux uucp www-data '_*'


# use generic completion for programs that support --help
compdef _gnu_generic tail head cp mv df uname

compdef '_files -g "*.(#l)(tar.bz2|tbz2|tbz|tar.gz|tgz|tar.xz|txz|tar.lzma|tar|rar|lha|lzh|7z|zip|jar|war|ear|xpi|deb|bz2|gz|Z|xz|lzma)"' extract

compdef '_files -g "*.(pdf|ps|djv(|u))"' evince
compdef '_files -g "*.(pdf|ps|djv(|u))"' zathura

# menu selection: pick item but stay in the menu (press esc-return)
# bindkey -M menuselect '\e^M' accept-and-menu-complete
bindkey -M menuselect '\e^M' accept-and-infer-next-history
# bindkey -M menuselect '^\n'  accept-and-menu-complete # conflicts with '^J'
bindkey -M menuselect '\eQ13;5~'  accept-and-menu-complete

bindkey -M menuselect '^j' down-line-or-search
bindkey -M menuselect '^k' up-line-or-search
bindkey -M menuselect '^l' forward-char
bindkey -M menuselect '^h' backward-char
bindkey -M menuselect '^d' forward-word
bindkey -M menuselect '^u' backward-word

# remove last inserted match
bindkey -M menuselect '^w' undo

# http://stackoverflow.com/a/844299/102250
# show dots while completion is working
expand-or-complete-with-dots() {
    echo -ne "\e[31m......\e[0m"
    zle expand-or-complete
    zle redisplay
}
zle -N expand-or-complete-with-dots
bindkey "^I" expand-or-complete-with-dots

# force file completion
zle -C complete-files complete-word _generic
zstyle ':completion:complete-files:*' completer _files
bindkey "^X^F" complete-files
# allow the key sequence to continue cycling in menu
bindkey -M menuselect "^X^F" complete-word

## complete word from tmux pane(s)
# http://blog.plenz.com/2012-01/zsh-complete-words-from-tmux-pane.html
# https://gist.github.com/blueyed/6856354
_tmux_pane_words() {
    if [[ -z "$TMUX_PANE" ]]; then
        _message "not running inside tmux!"
        return 1
    fi

    local expl
    local -U -a words

    # capture current pane first
    words=( ${(u)=$(tmux capture-pane -J -p)} )
    for i in $(tmux list-panes -F '#P'); do
        # skip current pane (handled above)
        [[ "$TMUX_PANE" = "$i" ]] && continue
        words+=( ${(u)=$(tmux capture-pane -J -p -t $i)} )
    done
    _wanted values expl 'words from current tmux pane' compadd -a words
}
zle -C current-screen-words-prefix   complete-word _generic
zle -C current-screen-words-anywhere complete-word _generic
bindkey '^[o' current-screen-words-prefix
bindkey -M menuselect '^[o' complete-word
bindkey '^[O' current-screen-words-anywhere
bindkey -M menuselect '^[O' complete-word
zstyle ':completion:current-screen-words-(prefix|anywhere):*' completer _tmux_pane_words
zstyle ':completion:current-screen-words-(prefix|anywhere):*' ignore-line current
zstyle ':completion:current-screen-words-anywhere:*' matcher-list 'b:=* m:{A-Za-z}={a-zA-Z}'

zle -C extpwds menu-complete _generic
bindkey "^X^T" extpwds
zstyle ':completion:extpwds::::' completer _extpwds

# vim: filetype=zsh
