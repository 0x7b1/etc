# CTRL-T - Paste the selected file path(s) into the command line
__fsel() {
    set -o nonomatch
    command find * \( -path '*/\.*' -o -name target \) -prune \
        -o -type f -print \
        -o -type d -print \
        -o -type l -print 2> /dev/null | fzf --multi | while read item; do
            printf '%q ' "$item"
        done
    echo
}

fzf-file-widget() {
    LBUFFER="${LBUFFER}$(__fsel)"
    zle redisplay
}
zle     -N   fzf-file-widget
bindkey '^T' fzf-file-widget

# ALT-C - cd into the selected directory
fzf-cd-widget() {
    cd "${$(set -o nonomatch; \
        command find * \( -path '*/\.*' -o -path '*/ivy/*.cache' -o -name target \) -prune -o -type d -print 2> /dev/null \
        | fzf):-.}"
    for func in $precmd_functions; do
        $func
    done
    zle reset-prompt
}
zle     -N    fzf-cd-widget
bindkey '\ec' fzf-cd-widget

# CTRL-R - Paste the selected command from history into the command line
if have fzf; then
    fzf-history-widget() {
        LBUFFER=$(fc -l 1 | fzf --exact --no-sort --nth=2.. --tac | sed "s/^ *[0-9*]* *//")
        zle redisplay
    }
    zle -N fzf-history-widget

    # allow nesting of C-R, from
    # http://chneukirchen.org/blog/archive/2013/03/10-fresh-zsh-tricks-you-may-not-know.html
    autoload -Uz narrow-to-region
    _history-incremental-preserving-pattern-search-backward() {
        NUMERIC=0 zle set-local-history
        local state
        MARK=CURSOR  # magick, else multiple ^R don't work
        narrow-to-region -p "$LBUFFER${BUFFER:+>>}" -P "${BUFFER:+<<}$RBUFFER" -S state
        zle end-of-history
        zle fzf-history-widget
        narrow-to-region -R state
    }
    zle -N _history-incremental-preserving-pattern-search-backward
    bindkey '^r' _history-incremental-preserving-pattern-search-backward
fi


RECENTDIRS_FILE="$ZSH_VAR_LIB_DIR/recent-dirs"
chpwd_add_recent_dir() {
    if [[ "$PWD" != $HOME ]]; then
        print -P '0\t%D{%s}\t1\t%~' >>| "$RECENTDIRS_FILE"
    fi
}
add-zsh-hook chpwd chpwd_add_recent_dir

j() {
    # adapted from http://chneukirchen.org/blog/archive/2017/01/zz-a-smart-and-efficient-directory-changer.html
    awk -v ${(%):-now='%D{%s}'} < "$RECENTDIRS_FILE" '
        BEGIN { FS="\t" }
        function r(t, f) {
            age = now - t
            return (age < 3600) ? f * 4 : (age < 86400) ? f * 2 : (age < 604800) ? f / 2 : f / 4
        }
        { f[$4] += $3; if ($2 > l[$4]) l[$4] = $2 }
        END { for (i in f) printf("%d\t%d\t%d\t%s\n", r(l[i], f[i]), l[i], f[i], i) }' | \
            sort -k2 -n -r | head -n 9000 | sort -n -r -o "$RECENTDIRS_FILE"
    cd ${~:-"$(cat "$RECENTDIRS_FILE" | cut -d$'\t' -f4- | fzf --exact --no-sort)"}
}

J() {
    cd "$(hash -d | sed -e 's/^.*=//' | fzf --exact)"
}

v() {
    local viminfo="$HOME/.var/lib/vim/viminfo"

    sed -r -n -e 's/^> //p'  ~/.var/lib/vim/viminfo | tac | while read line; do
        [[ -f "${line/\~/$HOME/}" ]] || continue
        [[ "${line//*\//}" == COMMIT_EDITMSG ]] && continue

        echo $line
    done | fzf
}

copy-pw() {
    lpass show --clip --password $(lpass ls | fzf | awk '{print $(NF)}' | sed 's/\]//g')
}

# vim: filetype=zsh
