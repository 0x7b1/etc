# CTRL-T - Paste the selected file path(s) into the command line
__fsel() {
    set -o nonomatch
    command find * -path '*/\.*' -prune \
        -o -type f -print \
        -o -type d -print \
        -o -type l -print 2> /dev/null | fzf -m | while read item; do
            printf '%q ' "$item"
        done
    echo
}

fzf-file-widget() {
    LBUFFER="${LBUFFER}$(__fsel)"
    zle redisplay
}
zle     -N   fzf-file-widget
bindkey '^T' fzf-file-widget

# ALT-C - cd into the selected directory
fzf-cd-widget() {
    cd "${$(set -o nonomatch; command find * -path '*/\.*' -prune \
            -o -type d -print 2> /dev/null | fzf):-.}"
    zle reset-prompt
}
zle     -N    fzf-cd-widget
bindkey '\ec' fzf-cd-widget

# CTRL-R - Paste the selected command from history into the command line
if have fzf; then
    fzf-history-widget() {
        LBUFFER=$(fc -l 1 | fzf -e +s +m -n2..,.. | sed "s/^ *[0-9*]* *//")
        zle redisplay
    }
    zle     -N   fzf-history-widget
    bindkey '^R' fzf-history-widget
fi


RECENTDIRS_FILE="$ZSH_VAR_LIB_DIR/recent-dirs"
__recentdirs_add() {
    echo "$PWD" >>| "$RECENTDIRS_FILE"

    # remove duplicate directories except for the last occurrence
    local tmpfile=$(mktemp)
    trap "rm -f '$tmpfile'" EXIT INT QUIT
    tac "$RECENTDIRS_FILE" | awk '!x[$0]++' | tac | tail -n 200 >| "$tmpfile"
    if [[ $? -eq 0 ]]; then
        mv "$tmpfile" "$RECENTDIRS_FILE"
    fi
}
add-zsh-hook chpwd __recentdirs_add

j() {
    cd "$(cat "$RECENTDIRS_FILE" | fzf -e -s +m)"
}

# vim: filetype=zsh
