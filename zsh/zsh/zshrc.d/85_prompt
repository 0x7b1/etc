__prompt_jobs() {
    echo "%(1j. ${pcol[bold]}${pcol[cyan]}%j${pcol[reset]}.)"
}

__prompt_exitcode() {
    echo "%(0?.. ${pcol[bold]}${pcol[red]}%?${pcol[reset]})"
}

# test if we have writing permission for the current directory
__prompt_wperm() {
    if ! [[ -w "$PWD" ]]; then
        echo "${pcol[bold]}${pcol[red]}!${pcol[reset]}"
    fi
}

__prompt_logo() {
    if [[ -z "$SSH_CONNECTION" ]]; then
        return
    fi
    if [[ -n "$LOGO" ]]; then
        echo "$LOGO • "
        return
    fi
    LOGO="?"
    if [[ -r /etc/issue ]]; then
        DISTRO=$(grep -m1 "^[a-zA-Z]" /etc/issue)
        case "${DISTRO:l}" in
            *debian*)
                LOGO="${pcol[161]}@${pcol[reset]}"
                ;;
            *ubuntu*)
                # LOGO="${pcol[160]}\\${pcol[208]}o${pcol[yellow]}/${pcol[reset]}"
                LOGO="${pcol[magenta]}u${pcol[reset]}"
                ;;
            *arch*)
                LOGO="${pcol[32]}A${pcol[reset]}"
                ;;
        esac
    elif ismac; then
        LOGO="${pcol[246]}X${pcol[reset]}"
    elif uname -s | grep -i netbsd >/dev/null; then
        LOGO="${pcol[244]}\\${pcol[202]}~${pcol[reset]}"
    fi
    echo "$LOGO • "
}

__prompt_orighome() {
    if [[ $HOME != $ORIGHOME ]]; then
        print "${pcol[246]}${PWD/$ORIGHOME/\~}${pcol[reset]}"
    fi
}

LOGO=""

PS1='
$(__prompt_logo)${pcol[167]}%n ${pcol[reset]}@ ${pcol[220]}%m${pcol[reset]} • ${pcol[green]}%<...<%~%<<${pcol[reset]}$(__prompt_wperm)$(__prompt_vcs)
%(2L.${pcol[bold]}%L${pcol[reset]} .)%(!.#.$) '

RPS1='$(__prompt_orighome)$(__prompt_exitcode)$(__prompt_jobs)'

PS2="${pcol[bold]}%_${pcol[reset]} %(!.#.>) "
PS3='?# '         # selection prompt used within a select loop.
PS4='+%N:%i:%_> ' # the execution trace prompt (setopt xtrace). default: '+%N:%i>'

# vim: filetype=zsh
