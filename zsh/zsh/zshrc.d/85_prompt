__prompt_jobs() {
    echo "%(1j. ${pcol[bold]}${pcol[cyan]}%j${pcol[reset]}.)"
}

__prompt_exitcode() {
    echo "%(0?.. ${pcol[bold]}${pcol[red]}%?${pcol[reset]})"
}

# test if we have writing permission for the current directory
__prompt_wperm() {
    if ! [[ -w "$PWD" ]]; then
        echo "${pcol[bold]}${pcol[red]}!${pcol[reset]}"
    fi
}

__prompt_logo() {
    if [[ -z "$SSH_CONNECTION" ]]; then
        return
    fi
    if [[ -n "$_prompt_logo" ]]; then
        echo "$_prompt_logo • "
        return
    fi
    _prompt_logo="?"
    if [[ -r /etc/issue ]]; then
        DISTRO=$(grep -m1 "^[a-zA-Z]" /etc/issue)
        case "${DISTRO:l}" in
            *debian*)
                _prompt_logo="${pcol[161]}@${pcol[reset]}"
                ;;
            *ubuntu*)
                # _prompt_logo="${pcol[160]}\\${pcol[208]}o${pcol[yellow]}/${pcol[reset]}"
                _prompt_logo="${pcol[magenta]}u${pcol[reset]}"
                ;;
            *arch*)
                _prompt_logo="${pcol[32]}A${pcol[reset]}"
                ;;
        esac
    elif ismac; then
        _prompt_logo="${pcol[246]}X${pcol[reset]}"
    elif uname -s | grep -i netbsd >/dev/null; then
        _prompt_logo="${pcol[244]}\\${pcol[202]}~${pcol[reset]}"
    fi
    echo "$_prompt_logo • "
}

__prompt_orighome() {
    if [[ $HOME != $ORIGHOME ]]; then
        print "${pcol[246]}${PWD/$ORIGHOME/\~}${pcol[reset]}"
    fi
}

__prompt_jdk() {
    [[ -z $JAVA_HOME ]] && return
    local jdk=$(basename $JAVA_HOME)
    if [[ $jdk != jdk-default ]]; then
        print "${pcol[cyan]}$jdk${pcol[reset]} "
    fi
}

_prompt_logo=""
_prompt_path="${pcol[green]}%~${pcol[reset]}"
_prompt_vcs_info=""

PS1='
$(__prompt_logo)${pcol[167]}%n ${pcol[reset]}@ ${pcol[220]}%m${pcol[reset]} • ${_prompt_path}$(__prompt_wperm)${_prompt_vcs_info}
%(2L.${pcol[bold]}%L${pcol[reset]} .)$(__prompt_jdk)%(!.#.$) '

RPS1='$(__prompt_orighome)$(__prompt_exitcode)$(__prompt_jobs)'

PS2="${pcol[bold]}%_${pcol[reset]} %(!.#.>) "
PS3='?# '         # selection prompt used within a select loop.
PS4='+%N:%i:%_> ' # the execution trace prompt (setopt xtrace). default: '+%N:%i>'

# vim: filetype=zsh
