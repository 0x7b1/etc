# adapted from
# http://zsh.sourceforge.net/Contrib/startup/users/debbiep/dot.zshenv
rationalize_path() {
    # Note that this works only on arrays, not colon-delimited strings.
    # Not that this is a problem now that there is typeset -T.
    local dir
    local build
    build=()
    # Evil quoting to survive an eval and to make sure that
    # this works even with variables containing IFS characters, if I'm
    # crazy enough to setopt shwordsplit.
    eval '
    foreach dir in "$'"$1"'[@]"
    do
        if [[ -n "$dir" && "$dir" != "." && -d "$dir" && -x "$dir" ]]; then
            dir=$(cd "$dir" && pwd -P)
            build=("$build[@]" "$dir")
        fi
    done
    '"$1"'=( "$build[@]" )
    '
}

if [[ -d /opt/intel ]]; then
    source /opt/intel/cc/10.0.023/bin/iccvars.sh
    source /opt/intel/idb/10.0.023/bin/idbvars.sh
fi

path=(
    "$HOME/bin"
    "$HOME/.etc/bin"
    "$HOME/usr/bin"
    "$HOME/.local/bin"

    /var/lib/gems/1.8/bin/

    # Macports
    /opt/local/bin
    /opt/local/sbin

    "$path[@]"

    /usr/local/sbin
    /sbin
    /usr/sbin
)
rationalize_path path
typeset -gxU path

fpath=(
    "$ZDOTDIR/func"
    "$HOME/.etc/zsh/zsh-completions/src"
    "$fpath[@]"
)
rationalize_path fpath
typeset -gxU fpath

# cdpath=(.. ~)
rationalize_path cdpath
typeset -gxU cdpath

if [[ -z "$MANPATH" ]] && have manpath; then
    MANPATH="$(manpath)"
fi
manpath=(
    "$HOME/usr/man"
    "$HOME/usr/share/man"
    "$manpath[@]"
)
rationalize_path manpath
typeset -gxU manpath

typeset -gxT LIBRARY_PATH library_path
library_path=(
    "$library_path[@]"
)
rationalize_path library_path
typeset -gxU library_path

typeset -gxT LD_LIBRARY_PATH ld_library_path
ld_library_path=(
    "$HOME/lib"
    "$ld_library_path[@]"
)
rationalize_path ld_library_path
typeset -gxU ld_library_path

export XDG_DATA_HOME=$HOME/.local/share
# export XDG_DATA_DIRS=/usr/local/share/:/usr/share/
export XDG_CONFIG_HOME=$HOME/.config
# export XDG_CONFIG_DIRS=/etc/xdg
export XDG_CACHE_HOME=$HOME/.cache

# make sure the cache dir exists
[[ ! -d $XDG_CACHE_HOME ]] && mkdir $XDG_CACHE_HOME
[[ ! -d $XDG_CACHE_HOME/zsh ]] && mkdir $XDG_CACHE_HOME/zsh

# vim: filetype=zsh
