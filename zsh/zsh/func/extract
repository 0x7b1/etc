# extract files into a separate directory automatically

emulate -L zsh

setopt extended_glob
setopt no_clobber
setopt err_return

if [[ -z "$1" ]]; then
    echo "usage: $0 <file>"
    return 1
fi

TMPDIR=zsh-extract-$RANDOM
ARCHIVEDIR="$(readlink -f ${1:h})"
ARCHIVEFILE="${1:t}"
BASENAME=${ARCHIVEFILE:r}
MOVE=no

mkdir $TMPDIR

case "$1" in
    *.tar.bz2)
        BASENAME=${BASENAME:r} ;&
    *.tbz2|*.tbz)
        COMMAND="tar xvj -f '$1' -C $TMPDIR"
        ;;
    *.tar.gz)
        BASENAME=${BASENAME:r} ;&
    *.tgz)
        COMMAND="tar xvz -f '$1' -C $TMPDIR"
        ;;
    *.tar.xz|*.tar.lzma)
        BASENAME=${BASENAME:r} ;&
    *.txz)
        COMMAND="tar xvJ -f '$1' -C $TMPDIR"
        ;;
    *.tar)
        COMMAND="tar xv -f '$1' -C $TMPDIR"
        ;;
    *.rar)
        COMMAND="unrar x '${ARCHIVEFILE}'"
        MOVE=yes
        ;;
    *.lha|*.lzh)
        COMMAND="lha x '${ARCHIVEFILE}'"
        MOVE=yes
        ;;
    *.7z)
        COMMAND="7z x -o$TMPDIR '$1'"
        ;;
    *.jar|*.war|*.ear|*.xpi|*.zip)
        COMMAND="unzip -d $TMPDIR '$1'"
        ;;
    *.deb)
        COMMAND="ar -x '${ARCHIVEFILE}'"
        MOVE=yes
        ;;
    *.bz2)
        COMMAND="bzip2 -d -c '$1' > '$BASENAME'"
        ;;
    *.gz|*.Z)
        COMMAND="gzip -d -c '$1' > '$BASENAME'"
        ;;
    *.xz|*.lzma)
        COMMAND="xz -d -c '$1' > '$BASENAME'"
        ;;
    *)
        echo "Unsupported archive type"
        rmdir $TMPDIR
        return 1
        ;;
esac

if [[ $MOVE == yes ]]; then
    mv "$1" $TMPDIR
    cd $TMPDIR
    eval "$COMMAND"
    RET=$?
    mv "$ARCHIVEFILE" "$ARCHIVEDIR"
    cd ..
else
    eval "$COMMAND"
    RET=$?
fi

if (( $RET != 0 )); then
    rmdir $TMPDIR
    return $RET
fi

echo

NUMFILES=$(ls $TMPDIR | wc -l)
if (( $NUMFILES == 0 )); then
    rmdir $TMPDIR
    echo "Extracted file to \"$BASENAME\""
elif (( $NUMFILES == 1 )); then
    if ! [[ -e "BASENAME" ]]; then
        FILE="$(basename $TMPDIR/*)"
        mv "$TMPDIR/$FILE" .
        rmdir $TMPDIR
        echo "Extracted \"$FILE\" to current directory"
    else
        echo "Extracted \"$FILE\" to directory \"$TMPDIR\""
    fi
else
    if ! [[ -e "BASENAME" ]]; then
        mv $TMPDIR "$BASENAME"
        echo "Extracted files to directory \"$BASENAME\""
    else
        echo "Extracted files to directory \"$TMPDIR\""
    fi
fi

# vim: filetype=zsh
