# extract files into a separate directory automatically

emulate -L zsh

setopt extended_glob
setopt no_clobber

if [[ -z "$1" ]]; then
    echo "usage: $0 <file>"
    return 1
fi

TMPDIR=zsh-extract-$RANDOM
ARCHIVEDIR="${1:h}"
ARCHIVEFILE="${1:t}"
BASENAME=""
MOVE=no
mkdir $TMPDIR

case "$1" in
    *.tar.bz2)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.tar.bz2}} ;&
    *.tbz2)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.tbz2}} ;&
    *.tbz)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.tbz}}
        COMMAND="tar xvj -f '$1' -C $TMPDIR"
        ;;
    *.tar.gz)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.tar.gz}} ;&
    *.tgz)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.tgz}}
        COMMAND="tar xvz -f '$1' -C $TMPDIR"
        ;;
    *.tar.xz)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.tar.xz}} ;&
    *.txz)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.txz}} ;&
    *.tar.lzma)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.tar.lzma}}
        COMMAND="tar xvJ -f '$1' -C $TMPDIR"
        ;;
    *.tar)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.tar}}
        COMMAND="tar xv -f '$1' -C $TMPDIR"
        ;;
    *.rar)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.rar}}
        COMMAND="unrar x '${ARCHIVEFILE}'"
        MOVE=yes
        ;;
    *.lzh)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.lzh}}
        COMMAND="lha x '${ARCHIVEFILE}'"
        MOVE=yes
        ;;
    *.7z)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.7z}}
        COMMAND="7z x -o$TMPDIR '$1'"
        ;;
    *.zip)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.zip}} ;&
    *.jar)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.jar}}
        COMMAND="unzip -d $TMPDIR '$1'"
        ;;
    *.deb)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.deb}}
        COMMAND="ar -x '${ARCHIVEFILE}'"
        MOVE=yes
        ;;
    *.bz2)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.bz2}}
        COMMAND="bzip2 -d -c '$1' > '$BASENAME'"
        ;;
    *.gz)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.gz}} ;&
    *.Z)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.Z}}
        COMMAND="gzip -d -c '$1' > '$BASENAME'"
        ;;
    *.xz)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.xz}} ;&
    *.lzma)
        BASENAME=${BASENAME:-${${ARCHIVEFILE}%.lzma}}
        COMMAND="xz -d -c '$1' > '$BASENAME'"
        ;;
    *)
        echo "Unsupported archive type"
        rmdir $TMPDIR
        return 1
        ;;
esac

if [[ $MOVE == yes ]]; then
    mv "$1" $TMPDIR && cd $TMPDIR && eval "$COMMAND"
    RET=$?
    mv "$ARCHIVEFILE" "$ARCHIVEDIR" && cd ..
else
    eval "$COMMAND"
    RET=$?
fi

if (( $RET != 0 )); then
    rmdir $TMPDIR
    return $RET
fi

echo

NUMFILES=$(ls $TMPDIR | wc -l)
if (( $NUMFILES == 0 )); then
    rmdir $TMPDIR
    echo "Extracted file to \"$BASENAME\""
elif (( $NUMFILES == 1 )); then
    if ! [[ -e "BASENAME" ]]; then
        FILE="$(basename $TMPDIR/*)"
        mv "$TMPDIR/$FILE" .
        rmdir $TMPDIR
        echo "Extracted \"$FILE\" to current directory"
    else
        echo "Extracted \"$FILE\" to directory \"$TMPDIR\""
    fi
else
    if ! [[ -e "BASENAME" ]]; then
        mv $TMPDIR "$BASENAME"
        echo "Extracted files to directory \"$BASENAME\""
    else
        echo "Extracted files to directory \"$TMPDIR\""
    fi
fi

# vim: filetype=zsh
