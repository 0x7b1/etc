#!/usr/bin/env zsh
# heavily inspired by byobu

items="cputemp battery"

TMUX_LIBDIR=$HOME/.etc/tmux/lib

if [[ -r $HOME/.local/share/tmux-statusrc ]]; then
    . $HOME/.local/share/tmux-statusrc
fi

ismac() {
    [[ $OSTYPE == darwin* ]] && return 0
    return 1
}

status_freq() {
    case "$1" in
        battery) echo 61 ;;
        cputemp) echo 19 ;;
    esac
}

get_status() {
    function="$1"

    local cachepath="$TMUX_CACHE/status/$function"
    local lastpath="$TMUX_CACHE/.last/$function"
    local lastrun
    [[ -r "$lastpath" ]] && read lastrun < "$lastpath" || lastrun=0

    freq=$(status_freq "$function")
    local expiry=$(($lastrun+$freq))

    . $TMUX_LIBDIR/$function

    # Update cache now, if necessary
    if (( $NOW >= $expiry )) || (( $lastrun == 0 )); then
        "__$function" > "$cachepath"
        echo "$NOW" > "$lastpath"
    fi
    if [[ -s "$cachepath" ]]; then
        IFS= read line < "$cachepath"; printf "$line"
    fi
}

NOW=$(print -P "%D{%s}")

for item in ${(s: :)items}; do
    get_status "$item"
done
